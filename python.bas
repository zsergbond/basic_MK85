// Игра "Змейка" для микрокомпьютера "Электроника-МК85"

1 Z = 3 : X = 9 : Y = 3 : W = 0   // Z - ДЛИННА ЗМЕЙКИ, МАКСИМУМ 9; X,Y НАЧАЛЬНЫЕ КООРДИНАТЫ ГОЛОВЫ; W - ВРЕМЕННАЯ ПЕРЕМЕННАЯ
2 FOR V=O TO Z*2-1 STEP 2
3 A(V) = X - W
4 A(V+1) = 3
5 W = W + 1
6 NEXT V

20 U=2   // U - НАПРАВЛЕНИЕ ДВИЖЕНИЯ 1 - ВВЕРХ, 2 - ВПРАВО, 3 - ВНИЗ, 4 - ВЛЕВО

100 GOSUB 490  // ВЫЗОВ ПОДПРОГРАММЫ ПРОРИСОВКИ ЭКРАНА
110 GOSUB 300  // ВЫЗОВ ПОДПРОГРАММЫ ПРОВЕРКИ НАЖАТИЯ КЛАВИШ 
120 GOSUB 710  // ВЫЗОВ ПОДПРОГРАММЫ ИЗМЕНЕНИЯ КООРДИНАТ ГОЛОВЫ ЗМЕЙКИ
130 GOSUB 1000 // ВЫЗОВ ПОДПРОГРАММЫ ОБНОВЛЕНИЯ МАССИВА КООРДИНАТ ЗМЕЙКИ
140 GOTO 100

// ОТСЛЕЖИВАНИЕ НАЖАТИЙ КЛАВИАТУРЫ
300 $=KEY
310 IF $=CHR 54; U=2:RETURN       //ЕСЛИ НАЖАТА "6"
320 IF $=CHR 52; U=4:RETURN       //ЕСЛИ НАЖАТА "4"
330 IF $=CHR 56; U=1:RETURN       //ЕСЛИ НАЖАТА "8"
340 IF $=CHR 50; U=3:RETURN       //ЕСЛИ НАЖАТА "2"
360 RETURN

// ПРОРИСОВКА ЭКРАНА
490 CSR 0,
500 FOR V=0 TO Z*2-1 STEP 2
510 DRAW A(V),A(V+1)
520 NEXT V
530 RETURN

// ИЗМЕНЕНИЕ КООРДИНАТ В ЗАВИСИМОСТИ ОТ НАПРАВЛЕНИЯ
710 IF U=2; X=X+1:GOTO 750    
720 IF U=4; X=X-1:GOTO 760
730 IF U=1; Y=Y+1:GOTO 770
740 IF U=3; Y=Y-1:GOTO 780
750 IF X>59; X=0 : RETURN
755 RETURN
760 IF X<0; X=59 : RETURN
765 RETURN
770 IF Y>6; Y=0 : RETURN
775 RETURN
780 IF Y<0; Y=6 : RETURN
785 RETURN

// РАСЧЁТ ОСТАЛЬНЫХ КООРДИНАТ ТЕЛА ЗМЕЙКИ 
1000 FOR I = Z*2-1 TO 3 STEP -2
1010 A(I-1) = A(I-3)
1020 A(I) = A(I-2)
1030 NEXT I
1040 A(0) = X
1050 A(1) = Y
1060 RETURN
