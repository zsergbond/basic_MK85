// Игра "ПИТОН" для микрокомпьютера "Электроника-МК85"

1 VAC: Z = 3 : X = 9 : Y = 3 : W = 0   // Z - ДЛИННА ЗМЕЙКИ, МАКСИМУМ 9; X,Y НАЧАЛЬНЫЕ КООРДИНАТЫ ГОЛОВЫ; W - ВРЕМЕННАЯ ПЕРЕМЕННАЯ
2 FOR V=O TO Z*2-1 STEP 2   // РАСЧИТЫВАЮ КООРДИНАТЫ ЗМЕЙКИ В ЗАВИСИМОСТИ ОТ ДЛИННЫ - Z
3 A(V) = X - W
4 A(V+1) = 3
5 W = W + 1
6 NEXT V

7 DEFM 30   // ОПРЕДЕЛЯЕМ ЕЩЁ 20 ЯЧЕЕК ПАМЯТИ ЗА СЧЕТ ШАГОВ ПРОГРАММЫ
8 A(45) = 0 // КОЛИЧЕСТВО СЪЕДЕНЫХ КРОЛИКОВ

11 A(40) = 500 // СРАЗУ ПОСЛЕ СТАРТА РИСУЕМ ЗМЕЙКУ ПОЛНОСТЬЮ (КОСВЕННЫЙ АДРЕС), А ПОСЛЕ ПЕРВОЙ ПРОРИСОВКИ МЕНЯЕМ ЭТОТ АДРЕСС В 522 СТРОКЕ

20 U=6   // U - НАПРАВЛЕНИЕ ДВИЖЕНИЯ 8 - ВВЕРХ, 6 - ВПРАВО, 2 - ВНИЗ, 4 - ВЛЕВО

80 GOTO 800

90  GOSUB 400    // ОДИН РАЗ ПРИ СТАРТЕ ВЫЗЫВАЕМ ПОДПРОГРАММУ ОПРЕДЕЛЕНИЯ СЛУЧАЙНЫХ КООРДИНАТ ЗАЙЦА
100 GOSUB A(40)  // ВЫЗОВ ПОДПРОГРАММЫ ПРОРИСОВКИ ЭКРАНА
105 CSR 11,:PRINT A(45)
110 GOSUB 300  // ВЫЗОВ ПОДПРОГРАММЫ ПРОВЕРКИ НАЖАТИЯ КЛАВИШ 
120 GOSUB 700 + U  // ВЫЗОВ ПОДПРОГРАММЫ ИЗМЕНЕНИЯ КООРДИНАТ ГОЛОВЫ ЗМЕЙКИ С КОСВЕННОЙ АДРЕСАЦИЕЙ
130 GOSUB 990 // ВЫЗОВ ПОДПРОГРАММЫ ОБНОВЛЕНИЯ МАССИВА КООРДИНАТ ЗМЕЙКИ
135 GOSUB 390 // ОПРЕДЕЛЕНИЕ СТОЛКНОВЕНИЯ С КРОЛИКОМ
140 GOTO 100

// ОТСЛЕЖИВАНИЕ НАЖАТИЙ КЛАВИАТУРЫ
300 $=KEY
310 IF $=CHR 54; U=6:RETURN       //ЕСЛИ НАЖАТА "6"
320 IF $=CHR 52; U=4:RETURN       //ЕСЛИ НАЖАТА "4"
330 IF $=CHR 56; U=8:RETURN       //ЕСЛИ НАЖАТА "8"
340 IF $=CHR 50; U=2:RETURN       //ЕСЛИ НАЖАТА "2"
360 RETURN

390 IF A(0) != A(26);RETURN   // ПРОВЕРКА СТОЛКНОВЕНИЯ ПИТОНА С КРОЛИКОМ
392 IF A(1) != A(27);RETURN   // ПРИ ВВОДЕ ИЗМЕНИТЬ ЗНАК НЕРАВЕНСТВА
394 Z = Z + 1 : IF Z > 9; Z=9 
396 A(45) = A(45) + 1
400 A(26)=INT(RAN# * 39) : A(27) = INT(RAN# * 6)  // СЛУЧАЙНЫЕ КООРДИНАТЫ КРОЛИКА
410 DRAW A(26),A(27):RETURN 

// ПРОРИСОВКА ЭКРАНА, 
500 FOR V=0 TO Z*2-1 STEP 2
510 DRAW A(V),A(V+1)
520 NEXT V
522 A(40) = 540
530 RETURN
540 DRAW A(0),A(1)
550 DRAWC A(28),A(29)
560 RETURN

// ИЗМЕНЕНИЕ КООРДИНАТ В ЗАВИСИМОСТИ ОТ НАПРАВЛЕНИЯ

702 Y = Y - 1: GOTO 780
704 X = X - 1: GOTO 760
706 X = X + 1: GOTO 750
708 Y = Y + 1: GOTO 770
750 IF X>39; X=0 : RETURN
755 RETURN
760 IF X<0; X=39 : RETURN
765 RETURN
770 IF Y>6; Y=0 : RETURN
775 RETURN
780 IF Y < 0; Y = 6 : RETURN
785 RETURN

// РИСОВАНИЕ ЗАСТАВКИ ПИТОНА В НАЧАЛЕ ИГРЫ
800 A(46)="0743053": A(47)="FG2O07U": A(48)="0SB5AP7"
810 A(49)="07TALFO": A(50)="0JELANC": A(51)="0GSBLQV"
820 FOR I=O TO 5
830 CSR I+6:LETC A(I+46)
840 PRINT CHR 96;
850 NEXT I : STOP
860 GOTO 90


// РАСЧЁТ ОСТАЛЬНЫХ КООРДИНАТ ТЕЛА ЗМЕЙКИ 
 990 A(28) = A(Z*2-2)   // СОХРАНЯЕМ КООРДИНАТЫ ПОСЛЕДНЕГО ЗВЕНА ЗМЕЙКИ ДЛЯ ДАЛЬНЕЙШЕГО ЕГО СТИРАНИЯ 
 995 A(29) = A(Z*2-1)
1000 FOR I = Z*2-1 TO 3 STEP -2
1010 A(I-1) = A(I-3)
1020 A(I) = A(I-2)
1030 NEXT I
1040 A(0) = X
1050 A(1) = Y
1060 RETURN
